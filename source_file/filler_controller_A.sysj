FillerA
(
    input String signal m2c_signal_liquid_A;
    output signal c2m_signal_liquid_A;
    output signal c2m_signal_filler_A_idle;
    input signal c2m_signal_filler_A_check_and_start;

    input signal bottleAtPos2A, bottleAtPos2AFull, dosUnitAEvac, dosUnitAAtTarget;
    output signal valveInjectorAOnOff, valveInletAOnOff, dosUnitAValveRetract, dosUnitAValveExtend;
)->{

    signal openInjector, closeInjector;
    signal openInlet, closeInlet;

    {
        int status = Controller_State.INITIAL;

        loop
        {
            switch (status)
            {
                case Controller_State.INITIAL:
                    emit c2m_signal_liquid_A;

                    await(c2m_signal_filler_A_check_and_start);
                    status = Controller_State.CHECK_BOTTLE;

                    break;

                case Controller_State.CHECK_BOTTLE:
                    //not bottle here

                    present (bottleAtPos2A)
                    {
                        status = Controller_State.DO_PROCESS;
                    }
                    else
                    {
                        status = Controller_State.IDLE;
                    }
                    break;

                case Controller_State.DO_PROCESS:
                    System.out.println("filler A do process");
                    //check queue.GetCurrentIngredient();

                    FillerHandler.liquid_queue_A.PopOneBottle();

                    emit openInjector;

                    pause;

                    present (dosUnitAEvac)
                    {
                        abort (dosUnitAAtTarget) { sustain dosUnitAValveRetract;}
                    }
                    else
                    {}

                    emit closeInjector;
                    pause;

                    emit openInlet;
                    pause;

                    abort (dosUnitAEvac || bottleAtPos2AFull) { sustain dosUnitAValveExtend; }

                    // if the bottle is full, close inlet, open injector, let the liquid back to the bank.
                    present (bottleAtPos2AFull)
                    {
                        emit closeInlet;
                        emit openInjector;
                        pause;

                        abort (dosUnitAEvac) { sustain dosUnitAValveExtend; }
                    }
                    else
                    {}

                    emit closeInlet;

                    status = Controller_State.IDLE;
                    break;

                case Controller_State.IDLE:
                    emit c2m_signal_filler_A_idle;
                    await(c2m_signal_filler_A_check_and_start);

                    status = Controller_State.CHECK_BOTTLE;
                    break;

                default:
                    break;
            }
            pause;
        }
    }
    ||
    {
        loop
        {
            await(m2c_signal_liquid_A);
            //upate ingredient list

            FillerHandler.liquid_queue_A.PushOneQueue((String)#m2c_signal_liquid_A);
            pause;
        }
    }
    ||
    {
        loop {
            await (openInjector);

            abort (closeInjector) { sustain valveInjectorAOnOff; }

            pause;
        }
    }
    ||
    {
        loop {
            await (openInlet);

            abort (closeInlet) { sustain valveInletAOnOff; }

            pause;
        }
    }
}
