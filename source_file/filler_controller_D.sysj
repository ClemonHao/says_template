FillerD
(
    input String signal m2c_signal_liquid_D;
    output signal c2m_signal_liquid_D;
    output signal c2m_signal_filler_D_idle;
    input signal c2m_signal_filler_D_check_and_start;

	input signal bottleAtPos2D, bottleAtPos2DFull, dosUnitDEvac, dosUnitDAtTarget;
	output signal valveInjectorDOnOff, valveInletDOnOff, dosUnitDValveRetract, dosUnitDValveExtend;
)->{

    signal openInjector, closeInjector;
    signal openInlet, closeInlet;

    {
        int status = Controller_State.INITIAL;

        loop
        {
            switch (status)
            {
                case Controller_State.INITIAL:
                    emit c2m_signal_liquid_D;

                    await(c2m_signal_filler_D_check_and_start);
                    status = Controller_State.CHECK_BOTTLE;

                    break;

                case Controller_State.CHECK_BOTTLE:
                    //not bottle here

                    present (bottleAtPos2D)
                    {
                        status = Controller_State.DO_PROCESS;
                    }
                    else
                    {
                        status = Controller_State.IDLE;
                    }
                    break;

                case Controller_State.DO_PROCESS:
                    System.out.println("filler D do process");
                    //check queue.GetCurrentIngredient();

                    FillerHandler.liquid_queue_D.PopOneBottle();

                    emit openInjector;

                    pause;

                    present (dosUnitDEvac)
                    {
                        abort (dosUnitDAtTarget) { sustain dosUnitDValveRetract;}
                    }
                    else
                    {}

                    emit closeInjector;

                    pause;

                    emit openInlet;

                    pause;

                    abort (dosUnitDEvac || bottleAtPos2DFull) { sustain dosUnitDValveExtend; }

                    // if the bottle is full, close inlet, open injector, let the liquid back to the bank.
                    present (bottleAtPos2DFull)
                    {
                        emit closeInlet;
                        emit openInjector;
                        pause;

                        abort (dosUnitDEvac) { sustain dosUnitDValveExtend; }
                    }
                    else
                    {}

                    emit closeInlet;

                    status = Controller_State.IDLE;
                    break;

                case Controller_State.IDLE:
                    emit c2m_signal_filler_D_idle;
                    await(c2m_signal_filler_D_check_and_start);

                    status = Controller_State.CHECK_BOTTLE;
                    break;

                default:
                    break;
            }
            pause;
        }
    }
    ||
    {
        loop
        {
            await(m2c_signal_liquid_D);
            //upate ingredient list

            FillerHandler.liquid_queue_D.PushOneQueue((String)#m2c_signal_liquid_D);
            pause;
        }
    }
    ||
    {
        loop {
            await (openInjector);

            abort (closeInjector) { sustain valveInjectorDOnOff; }

            pause;
        }
    }
    ||
    {
        loop {
            await (openInlet);

            abort (closeInlet) { sustain valveInletDOnOff; }

            pause;
        }
    }
}
