FillerB
(
    input String signal m2c_signal_liquid_B;
    output signal c2m_signal_liquid_B;
    output signal c2m_signal_filler_B_idle;
    input signal c2m_signal_filler_B_check_and_start;

    input signal bottleAtPos2B, bottleAtPos2BFull, dosUnitBEvac, dosUnitBAtTarget;
    output signal valveInjectorBOnOff, valveInletBOnOff, dosUnitBValveRetract, dosUnitBValveExtend;
)->{

    signal openInjector, closeInjector;
    signal openInlet, closeInlet;

    {
        int status = Controller_State.INITIAL;
        loop
        {
            switch (status)
            {
                case Controller_State.INITIAL:
                    emit c2m_signal_liquid_B;

                    await(c2m_signal_filler_B_check_and_start);
                    status = Controller_State.CHECK_BOTTLE;

                    break;

                case Controller_State.CHECK_BOTTLE:
                    //not bottle here

                    present (bottleAtPos2B)
                    {
                        status = Controller_State.DO_PROCESS;
                    }
                    else
                    {
                        status = Controller_State.IDLE;
                    }
                    break;

                case Controller_State.DO_PROCESS:
                    System.out.println("filler B do process");
                    //check queue.GetCurrentIngredient();
                    FillerHandler.liquid_queue_B.PopOneBottle();

                    emit openInjector;

                    pause;

                    present (dosUnitBEvac)
                    {
                        abort (dosUnitBAtTarget) { sustain dosUnitBValveRetract;}
                    }
                    else
                    {}

                    emit closeInjector;

                    pause;

                    emit openInlet;

                    pause;

                    abort (dosUnitBEvac || bottleAtPos2BFull) { sustain dosUnitBValveExtend; }

                    // if the bottle is full, close inlet, open injector, let the liquid back to the bank.
                    present (bottleAtPos2BFull)
                    {
                        emit closeInlet;
                        emit openInjector;
                        pause;

                        abort (dosUnitBEvac) { sustain dosUnitBValveExtend; }
                    }
                    else
                    {}

                    emit closeInlet;


                    status = Controller_State.IDLE;
                    break;

                case Controller_State.IDLE:
                    emit c2m_signal_filler_B_idle;
                    await(c2m_signal_filler_B_check_and_start);

                    status = Controller_State.CHECK_BOTTLE;
                    break;

                default:
                    break;
            }
            pause;
        }
    }
    ||
    {
        loop
        {
            await(m2c_signal_liquid_B);
            //upate ingredient list

            FillerHandler.liquid_queue_B.PushOneQueue((String)#m2c_signal_liquid_B);
            pause;
        }
    }
    ||
    {
        loop {
            await (openInjector);

            abort (closeInjector) { sustain valveInjectorBOnOff; }

            pause;
        }
    }
    ||
    {
        loop {
            await (openInlet);

            abort (closeInlet) { sustain valveInletBOnOff; }

            pause;
        }
    }
}
